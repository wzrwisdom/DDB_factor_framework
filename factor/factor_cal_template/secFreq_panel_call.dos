def saveTb(input){
    data = input
    facDbName = "dfs://secondLevelDB"
    facTbName = "freq_6sec"  
    data[`username] = `wangzr
    data.reorderColumns!(`username`tradetime`securityid`factorname`value)
    loadTable(facDbName, facTbName).append!(data)
}

def secFreq_panel_call(dbName, tbName, startDate, endDate, factorName, securityidName, tradetimeName){
    // 该计算模板利用标准面板数据计算因子
    // 如数据列名与因子代码中参数名不同，请在使用本模板前，使用prepData计算模板映射

    // 找出因子函数所需参数
    syntax = facplf_find_func_params(factorName)

    // 数据准备
    rawdata = loadTable(dbName, tbName)
    selection = each(sqlCol, syntax).append!(sqlCol(tradetimeName)).append!(sqlCol(securityidName))
    whereConditions = [expr(startDate, <=,sqlCol(tradetimeName, date), <=, endDate)]
    whereConditions.append!(expr(09:30:00, !=, sqlCol(tradetimeName, time)))
    whereConditions.append!(expr(13:00:00, !=, sqlCol(tradetimeName, time)))
    data = sql(
        select = selection,
        from = rawdata, 
        where = whereConditions
    ).eval()
    
    //转置为面板数据
    panels = dict(syntax, panel(data[tradetimeName], data[securityidName], data[syntax]))
    
    //将数据入参
    args = array(ANY)
    for(syt in syntax){
        args.append!(panels[syt])
    }

    res = makeUnifiedCall(funcByName(factorName), args).eval()
    
    // 转为窄表存储
    colNum = size(res.columnNames())
    rowNum = size(res.rowNames())
    result = table(
                    take(res.rowNames(), rowNum*colNum) as `tradetime, 
                    stretch(res.columnNames(), rowNum*colNum) as `securityid,
                    take(factorName, rowNum*colNum) as `factorname,
                    res.flatten() as `value
                    )
    unifiedCall(saveTb, [result])
    return result
    
}